plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.6'
}

group = 'info.preva1l.fadah'
version = "2.8.3"

ext {
    set 'version', rootProject.version.toString()
    set 'configlib_version', configlib_version.toString()
    set 'rhino_version', rhino_version.toString()
    set 'acp2_version', acp2_version.toString()
    set 'redisson_version', redisson_version.toString()
    set 'hikari_version', hikari_version.toString()
    set 'sqlite_version', sqlite_version.toString()
    set 'mysql_version', mysql_version.toString()
    set 'mariadb_version', mariadb_version.toString()
    set 'mongo_version', mongo_version.toString()
    set 'influxdb_version', influxdb_version.toString()
    set 'minimessage_version', minimessage_version.toString()
    set 'mm_platform_version', mm_platform_version.toString()
}

allprojects {
    apply plugin: 'com.gradleup.shadow'
    apply plugin: 'maven-publish'
    apply plugin: 'java'

    sourceCompatibility = 21
    targetCompatibility = 21

    compileJava.options.encoding = 'UTF-8'
    javadoc.options.encoding = 'UTF-8'
    javadoc.options.addStringOption('Xdoclint:none', '-quiet')

    repositories {
        mavenLocal()
        mavenCentral()
        repositories {
            maven {
                url "https://repo.preva1l.info/releases/"
                name "FinallyADecent"
            }
        }
    }

    dependencies {
        compileOnly 'org.jetbrains:annotations:26.0.2'
        compileOnly 'org.projectlombok:lombok:1.18.36'
        annotationProcessor 'org.projectlombok:lombok:1.18.36'
        annotationProcessor 'org.jetbrains:annotations:26.0.2'

        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.12.0'
        testImplementation 'org.junit.jupiter:junit-jupiter-params:5.12.0'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.12.0'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.12.0'

        testCompileOnly 'org.jetbrains:annotations:26.0.2'
        testCompileOnly 'org.projectlombok:lombok:1.18.36'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.36'
    }

    test {
        useJUnitPlatform()
    }

    processResources {
        def tokenMap = rootProject.ext.properties
        filesMatching(['**/*.json', '**/plugin.yml']) {
            expand tokenMap
        }
    }
}

subprojects {
    version rootProject.version
    archivesBaseName = "${rootProject.name}-${project.name.capitalize()}"

    shadowJar {
        destinationDirectory.set(file("$rootDir/target"))
        archiveClassifier.set('')
    }

    java {
        withSourcesJar()
        withJavadocJar()
    }

    sourcesJar {
        destinationDirectory.set(file("$rootDir/target"))
    }

    javadocJar {
        destinationDirectory.set(file("$rootDir/target"))
    }

    shadowJar.dependsOn(sourcesJar, javadocJar)

    publishing {
        def user = findProperty("fad_username")
        def pass = findProperty("fad_password")

        if (project.name != "API") return

        if (!user || !pass) {
            println "Skipping publishing: Credentials are missing."
            return
        }

        repositories {
            maven {
                url = uri("https://repo.preva1l.info/releases/")
                name = "FinallyADecent"
                credentials {
                    username = user
                    password = pass
                }
            }
        }

        publications {
            create("mavenJava", MavenPublication) {
                groupId = 'info.preva1l'
                artifactId = 'fadah'
                version = "$rootProject.version"
                artifact shadowJar
                artifact sourcesJar
                artifact javadocJar
            }
        }
    }

    jar.dependsOn shadowJar
    project.delete "$rootDir/target"
}

logger.lifecycle("Building Fadah ${version}")